?start: modfile


modfile: (var_statement | par_statement | varexo_statement | pred_statement | parinit_statement)*   \
    model_block \
    (command_statement | steady_block | initval_block | steady_block | shocks_block )*
    

varexo_statement: "varexo " (lsymbol ","?)* lsymbol ";"
var_statement: "var " (lsymbol ","?)* lsymbol ";"
par_statement: "parameters " (lsymbol ","?)* lsymbol ";"

pred_statement: "predetermined_variables " (symbol ","?)* symbol ";"

?parinit_statement: parassignment ";"

lsymbol: symbol "$" latex_string "$" "(" properties ")"
    | symbol "(" properties ")"
    | symbol "$" latex_string "$"
    | symbol 

latex_string: LATEX_STRING


# TODO

command_statement:  COMMAND ";"
    | COMMAND "(" symbol ")" ";"
    | COMMAND (symbol *) ";" 
    | COMMAND "(" (symbol "=" NUMBER ",")* symbol "=" NUMBER ")" ";"
    | COMMAND "(" (symbol "=" NUMBER ",")* symbol "=" NUMBER ")" (symbol *) ";" 


model_block.1: "model" ";" _list_of_equations _END_TOKEN ";"
    | "model" "(" "linear" ")" ";" _list_of_equations _END_TOKEN ";"

steady_block.1: "steady_state_model" ";" _list_of_parassignments _END_TOKEN ";"

initval_block.1: "initval" ";" _list_of_parassignments _END_TOKEN ";"

shocks_block.1: "shocks" ";"  (setstdvar_stmt | setvar_stmt | setcovar_stmt)*  _END_TOKEN ";" 

setstdvar_stmt: "var" symbol ";" "stderr" formula ";"
setvar_stmt: "var" symbol "=" formula ";"
setcovar_stmt: "var" symbol "," symbol "=" formula ";"


_list_of_equations: (lequation) (";"  lequation )* ";"
_list_of_parassignments: (parassignment) (";"  parassignment )* ";"

lequation: equation
    | "[" properties "]" equation

properties: (pair ",")* pair
pair:  NAME "=" "'" UNQUOTED_STRING "'"

?equation: equality | formula

parassignment: symbol "=" formula

assignment: variable "=" formula

equality: sum "=" sum 
    | sum "==" sum

?formula: sum
?sum: product
    | sum "+" product   -> add
    | sum "-" product   -> sub
?product: atom
    | product "*" atom  -> mul
    | product "/" atom  -> div
?pow: atom _POW atom -> pow

_POW: "^"|"**"

?atom: NUMBER           -> number
        | "-" atom         -> neg
        | pow
        | call
        | symbol         
        | "(" sum ")"
        | variable


variable.1: cname  "[" date_index "]" -> variable
        | cname "[" "t" "]" -> variable
        | cname "(" date_index ")" -> variable
        | cname "[" "t$" subperiod "]"

subperiod: INT | NAME

!cname: NAME -> name

?date_index: "t" SIGNED_INT2 -> date
    | SIGNED_INT -> date

call: FUNCTION "(" sum ")"
    | NAME "(" (formula ",")* formula ")"


!symbol: NAME -> symbol


_END_TOKEN: "end"

FUNCTION: "sin"|"cos"|"exp"|"log"|"steady_state"

COMMAND: "steady"|"check"|"stoch_simul"

SIGNED_INT2: ("+"|"-") INT

_NEWLINE: NEWLINE

UNICODE_LETTER: /[^\W\d_\$]/

LATEX_STRING: /[^'"\$]+/

UNQUOTED_STRING: /[^'"]+/

NAME: UNICODE_LETTER ("_"|UNICODE_LETTER|DIGIT)*

#NAME: /(?!(end)\b)[^\W\d_\$]+/i

%import common.SIGNED_INT
%import common.DIGIT
%import common.INT
%import common.NUMBER
%import common.WS_INLINE
%import common.NEWLINE

MULTILINE_COMMENT: "/*" /(.|\n)+?/ "*/"
COMMENT: ("//"|"%"|"#") /[^\n]/*

%ignore MULTILINE_COMMENT
%ignore COMMENT
%ignore WS_INLINE
%ignore NEWLINE  // comment out to avoid crash